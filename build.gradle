buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
		maven {
            url 'http://maven.leviathan-studio.com/artifactory/plugins-release'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.0.0"
    }
}

// plugins
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

// Maven
version = "0.3.0"
group= "com.leviathanstudio"
archivesBaseName = "CraftStudioAPI"
ext.suffix="beta"

// Jenkins build
ext.buildnumber = 0
if (System.getenv().BUILD_NUMBER) {
    project.buildnumber = System.getenv().BUILD_NUMBER
    //version += "." + System.getenv().BUILD_NUMBER
} else {
    logger.lifecycle "SETTING BUILDNUMBER TO 0"
}

// Java
compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "1.9.4-12.17.0.2051"
    runDir = "run"
    mappings = "snapshot_20160627"
    replace "@VERSION@",  project.version
}

//Full version
ext.fullVersion="${project.version}.${project.buildnumber}-mc${project.minecraft.version}"

if(ext.suffix != "") {
	ext.fullVersion += "-${project.ext.suffix}"
}

repositories {
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java/'
        }
        resources {
            srcDirs 'src/main/resources/'
        }
    }
    mod {
        java {
            srcDirs 'src/mod/java/'
        }
        resources {
            srcDirs 'src/mod/resources/'
        }
    }
}

dependencies {
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    // resources
    from(sourceSets.main.resources.srcDirs) {
        include 'com/**/**'
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {    
    manifest {
        attributes  'group':project.group
    }
    exclude 'com/leviathanstudio/craftstudio/dev/**'
    appendix = 'universal'
    version = "${project.ext.fullVersion}"
}

task devJar(type: Jar) {
    from(sourceSets.main.allSource) {
        include 'com/**/**'
    }
    from(sourceSets.main.output) {
        include '**'
    }
    manifest {
        attributes  'group':project.group
    }
    classifier = 'dev'
    version = "${project.ext.fullVersion}"
}

// Publish the library
artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        version = "${project.ext.fullVersion}"
        repository {
            repoKey = 'leviathan'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publications ('mavenJava')
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact (devJar) { 
                classifier = 'dev'
            }
        }
    }
}

build.dependsOn devJar
